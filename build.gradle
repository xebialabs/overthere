// build.gradle for Overthere
buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.5.0'
  }
}

defaultTasks 'build'

version = '2.0.0-SNAPSHOT'
group = 'com.xebialabs.overthere'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'license'

repositories {
  mavenCentral()
  maven {
    url 'http://download.java.net/maven/2/'
  }
  maven {
    url 'https://oss.sonatype.org/content/repositories/releases'
  }
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

configurations {
  compile {
    transitive = false
  }
}

dependencies {
  // General dependencies
  compile 'com.google.guava:guava:10.0.1'
  compile 'nl.javadude.scannit:scannit:1.0.2'
  compile 'org.slf4j:slf4j-api:1.6.3'

  // SSH
  compile 'net.schmizz:sshj:0.7.0'
  runtime 'com.jcraft:jzlib:1.0.7'
  runtime 'org.bouncycastle:bcprov-jdk16:1.45'

  // CIFS
  compile 'jcifs:jcifs:1.3.17'

  // Telnet 
  compile 'commons-net:commons-net:2.0'

  // WinRM
  compile('dom4j:dom4j:1.6.1') {
    exclude group: 'xml-apis', module: 'xml-apis'
  }
  compile('jaxen:jaxen:1.1.1') {
    exclude group: 'xml-apis', module: 'xml-apis'
    exclude group: 'xalan', module: 'xalan'
    exclude group: 'xerces', module: 'xmlParserAPIs'
    exclude group: 'xerces', module: 'xercesImpl'
  }
  compile 'org.apache.httpcomponents:httpclient:4.2.1'
  compile 'org.apache.httpcomponents:httpcore:4.2.1'
  compile 'commons-codec:commons-codec:1.5'

  // Test dependencies
  testCompile 'com.xebialabs.cloud:overcast:1.1.0'
  testCompile 'org.hamcrest:hamcrest-core:1.2.1'
  testCompile 'org.hamcrest:hamcrest-library:1.2.1'
  testCompile 'org.mockito:mockito-core:1.8.5'
  testCompile 'org.testng:testng:5.14.10'
  testCompile 'nl.javadude.assumeng:assumeng:1.2.2'

  testRuntime 'ch.qos.logback:logback-classic:0.9.30'
  testRuntime('org.uncommons:reportng:1.1.2') {
    exclude group: 'org.testng', module: 'testng'
  }
}

test {
  useTestNG()
  options {
    listeners << 'org.uncommons.reportng.JUnitXMLReporter'
  }
  excludes = ['**/*Itest.*']
}

task itest(type: Test) {
  useTestNG {
    suites "testng.xml"
  }
  options {
    listeners << 'org.uncommons.reportng.JUnitXMLReporter'
  }

  includes = ['**/*Itest.*']
  testResultsDir = file("${buildDir}/itest-results")
  testReportDir = file("${buildDir}/reports/itests")

  copyProjectPropertyToSystemProperty(project, systemProperties, 'itests')
  copyProjectPropertyToSystemProperty(project, systemProperties, 'aws.endpoint')
  copyProjectPropertyToSystemProperty(project, systemProperties, 'aws.accessKey')
  copyProjectPropertyToSystemProperty(project, systemProperties, 'aws.secretKey')
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar, javadocJar
}

license {
  header project.file('HEADER')
}

signing {
  required { gradle.taskGraph.hasTask('uploadArchives') }
  sign configurations.archives
}

uploadArchives {
  if(project.hasProperty('sonatypeUsername')) {
    repositories.mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      configuration = configurations.archives

      repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }
      snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      pom.project {
        name 'Overthere'
        description 'Remote file manipulation and process execution framework for Java'
        packaging 'jar'
        url 'https://github.com/xebialabs/overthere'
        inceptionYear '2011'

        scm {
          url 'https://github.com/xebialabs/overthere'
        }

        licenses {
          license {
            name 'GPLv2 with XebiaLabs FLOSS License Exception'
            url 'https://github.com/xebialabs/overthere/blob/master/LICENSE'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id 'vpartington'
            name 'Vincent Partington'
            url 'http://www.xebialabs.com/'
            email 'vpartington@xebialabs.com'
            roles {
              role 'Developer'
            }
          }
          developer {
            id 'hierynomus'
            name 'Jeroen van Erp'
            url 'http://www.xebialabs.com/'
            email 'jvanerp@xebialabs.com'
            roles {
              role 'Developer'
            }
          }
        }
      }
    }
  }
}

def copyProjectPropertyToSystemProperty(project, systemProperties, name) {
  if(project.hasProperty(name)) {
    systemProperties[name] = project.getProperty(name)
  }
}
