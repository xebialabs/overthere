/*
 * Release and publish instructions:
 *
 * Overthere can be automatically "closed" and "promoted" on maven central by calling gradle tasks
 * closeRepository, promoteRepository or closeAndPromoteRepository. Archives need to be uploaded manually.
 *
 * For example:
 * gradle clean build signArchives uploadArchives closeAndPromoteRepository
 *
 * prerequisites: Published PGP key installed and configured, authorised sonatype username and password configured
 */
buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
  }
}

// build.gradle for Overthere
plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "org.ajoberstar.release-opinion" version "1.5.1"
}
defaultTasks 'build'

group = 'com.xebialabs.overthere'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'license'
apply plugin: 'io.codearte.nexus-staging'

repositories {
  mavenCentral()
  maven {
    url 'http://download.java.net/maven/2/'
  }
  maven {
    url 'https://oss.sonatype.org/content/repositories/releases'
  }
  maven {
    url 'http://www.libvirt.org/maven2/'
  }
  mavenLocal()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
  compile {
    transitive = false
    exclude group: 'org.bouncycastle', module: 'bcprov-jdk16'
  }
}

if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}

release {
  grgit = org.ajoberstar.grgit.Grgit.open(project.projectDir)
  tagStrategy {
    toTagString = { vs -> "${project.name}-$vs" }
    parseTag = { org.ajoberstar.grgit.Tag tag ->
      if (tag.name.startsWith(project.name)) {
        try {
          com.github.zafarkhaja.semver.Version.valueOf(tag.name[project.name.size() + 1..-1])
        } catch (Exception e) {
          null
        }
      } else {
        null
      }
    }
  }
}

dependencies {
  // General dependencies
  compile 'nl.javadude.scannit:scannit:1.4.1'
  compile 'org.slf4j:slf4j-api:1.7.12'
  compile 'org.slf4j:jcl-over-slf4j:1.7.12'

  // SSH
  compile 'com.hierynomus:sshj:0.27.0'
  runtime 'com.jcraft:jzlib:1.0.7'

  // CIFS
  compile 'jcifs:jcifs:1.3.17'

  // SMB2/3
  compile 'com.hierynomus:smbj:0.9.1'
  compile 'net.engio:mbassador:1.3.0'
  compile "org.bouncycastle:bcprov-jdk15on:1.60"
  compile "org.bouncycastle:bcpkix-jdk15on:1.60"

  // Telnet
  compile 'commons-net:commons-net:3.3'

  // WinRM
  compile('dom4j:dom4j:1.6.1') {
    exclude group: 'xml-apis', module: 'xml-apis'
  }
  compile('jaxen:jaxen:1.1.6') {
    exclude group: 'xml-apis', module: 'xml-apis'
    exclude group: 'xalan', module: 'xalan'
    exclude group: 'xerces', module: 'xmlParserAPIs'
    exclude group: 'xerces', module: 'xercesImpl'
  }
  compile('org.apache.httpcomponents:httpclient:4.4.1') {
    exclude group: 'commons-logging', module: 'commons-logging'
  }
  compile 'org.apache.httpcomponents:httpcore:4.4.1'
  compile 'commons-codec:commons-codec:1.10'

  // Test dependencies
  testCompile('com.xebialabs.cloud:overcast:2.5.1') {
    exclude group: 'com.xebialabs.overthere', module: 'overthere'
    exclude group: 'net.schmizz', module: 'sshj'
  }
  testCompile 'org.hamcrest:hamcrest-core:1.3'
  testCompile 'org.hamcrest:hamcrest-library:1.3'
  testCompile 'org.mockito:mockito-core:1.8.5'
  testCompile('org.testng:testng:5.14.10') {
    exclude group: "junit", module: "junit"
  }
  testCompile('nl.javadude.assumeng:assumeng:1.2.2') {
    exclude group: "junit", module: "junit"
  }
  testCompile 'com.google.guava:guava:16.0.1'

  testRuntime 'ch.qos.logback:logback-classic:1.0.6'
}

tasks.withType(Test) {
  afterSuite { descriptor, result ->
    if (descriptor.className != null) {
      def indicator = "\u001B[32m✓\u001b[0m"
      if (result.failedTestCount > 0) {
        indicator = "\u001B[31m✘\u001b[0m"
      }
      logger.lifecycle("$indicator Test ${descriptor.name}; Executed: ${result.testCount}/\u001B[32m${result.successfulTestCount}\u001B[0m/\u001B[31m${result.failedTestCount}\u001B[0m")
    }
  }
}

test {
  useTestNG()

  excludes = ['**/*Itest.*']

  maxHeapSize = "512m"
}

task itest(type: Test) {
  useTestNG()

  includes = ['**/*Itest.*', '**/LocalConnectionTest.*']
  reports.junitXml.destination = file("${buildDir}/itest-results")
  reports.html.destination = file("${buildDir}/reports/itests")

  maxHeapSize = "512m"
  copyProjectPropertyToSystemProperty(project, systemProperties, 'itests')

  copyProjectPropertyToSystemProperty(project, systemProperties, 'test.overthere.linux.regular.password')
  copyProjectPropertyToSystemProperty(project, systemProperties, 'test.overthere.linux.regular.passphrase')
  copyProjectPropertyToSystemProperty(project, systemProperties, 'test.overthere.linux.regular.privatekey')
  copyProjectPropertyToSystemProperty(project, systemProperties, 'test.overthere.linux.root.password')
  copyProjectPropertyToSystemProperty(project, systemProperties, 'test.overthere.linux.untrusted.password')
  copyProjectPropertyToSystemProperty(project, systemProperties, 'test.overthere.linux.trusted.privatekey')
  copyProjectPropertyToSystemProperty(project, systemProperties, 'test.overthere.windows.regular.password')
  copyProjectPropertyToSystemProperty(project, systemProperties, 'test.overthere.windows.administrative.password')
  copyProjectPropertyToSystemProperty(project, systemProperties, 'test.overthere.windows.domain.password')

}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar, javadocJar
}

license {
  header project.file('HEADER')
  ignoreFailures true
  exclude "**/ProxySocketFactory.java"
}

signing {
  required { gradle.taskGraph.hasTask('uploadArchives') }
  sign configurations.archives
}

nexusStaging {
  packageGroup = "com.xebialabs"
}

uploadArchives {
  if(project.hasProperty('sonatypeUsername')) {
    repositories.mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      configuration = configurations.archives

      repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }
      snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }

      pom.project {
        name 'Overthere'
        description 'Remote file manipulation and process execution framework for Java'
        packaging 'jar'
        url 'https://github.com/xebialabs/overthere'
        inceptionYear '2011'

        scm {
          url 'https://github.com/xebialabs/overthere'
        }

        licenses {
          license {
            name 'GPLv2 with XebiaLabs FLOSS License Exception'
            url 'https://github.com/xebialabs/overthere/blob/master/LICENSE'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id 'vpartington'
            name 'Vincent Partington'
            url 'http://www.xebialabs.com/'
            email 'vpartington@xebialabs.com'
            roles {
              role 'Developer'
            }
          }
          developer {
            id 'hierynomus'
            name 'Jeroen van Erp'
            url 'http://www.xebialabs.com/'
            email 'jvanerp@xebialabs.com'
            roles {
              role 'Developer'
            }
          }
        }
      }
    }
  }
}

def copyProjectPropertyToSystemProperty(project, systemProperties, name) {
  if(project.hasProperty(name)) {
    systemProperties[name] = project.getProperty(name)
  }
}

task dumpVersion << {
  file(buildDir).mkdirs()
  def out = file("$buildDir/version.dump")
  out.write("version=$project.version")
}
